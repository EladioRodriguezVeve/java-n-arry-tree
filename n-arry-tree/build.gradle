plugins {
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
    id 'signing'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addBooleanOption('html5', true)
}

repositories {
    mavenLocal()
    mavenCentral()
}

group = 'io.github.eladiorodriguezveve'
version = '1.0.0'

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    
    implementation 'com.google.guava:guava:23.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    
    api 'com.google.code.gson:gson:2.8.6'

}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('copyJavadoc', Copy) {
    dependsOn tasks.javadoc
	from "$docsDir/javadoc"
	into "$rootDir/javadocs"
}

tasks.named('jar') {
    manifest {
        attributes('Automatic-Module-Name': 'io.github.eladiorodriguezveve.n.arry.tree')
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'n-arry-tree'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'N-arrry Tree'
                description = 'Library for creating n-arry trees'
                url = 'https://github.com/EladioRodriguezVeve/n-arry-tree'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'EladioRodriguezVeve'
                        name = 'Eladio Rodriguez Veve'
                        email = 'eladiomail@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/EladioRodriguezVeve/n-arry-tree.git'
                    developerConnection = 'scm:git:https://github.com/EladioRodriguezVeve/n-arry-tree.git'
                    url = 'https://github.com/EladioRodriguezVeve/n-arry-tree/'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'ossrh'
            credentials(PasswordCredentials)
            url = 'https://s01.oss.sonatype.org/'
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}